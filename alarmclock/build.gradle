apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.bmuschko.docker-java-application'

mainClassName = "com.externalalarmclock.alarmclock.AlarmClockApplicationKt"

project.ext {
    configPath = "$project.projectDir/src/dist/conf/"
}

run {
 args 'server', configPath + 'alarmclock.yml'
}

dependencies {
    implementation 'io.dropwizard:dropwizard-core:2.0.16'
    implementation 'com.google.guava:guava:30.1-jre'
    implementation project(':lib.externalalarmclock')
    implementation project(':lib.rpiws281x')
    implementation 'io.github.dropwizard-jobs:dropwizard-jobs-core:4.0.0-RELEASE'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

repositories {
  mavenLocal()
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"
compileKotlin.kotlinOptions.jvmTarget = "1.8"
// UTF-8 should be standard by now. So use it!
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// WORKAROUND: Adding extra source set to allow docker to include configuration file.
sourceSets {
    main {
         resources {
             srcDirs "src/main/resources", "src/dist/config"
         }
    }
}

docker {
    javaApplication {
        baseImage = 'azul/zulu-openjdk-alpine:11-jre'
        maintainer = 'Michiel Leegwater <mleegwt@users.noreply.github.com>'
        ports = [ 8080 ]
    }
    dockerCreateDockerfile {
        defaultCommand 'server /app/resources/alarmclock.yml'
        entryPoint "java", "-cp", "/app/resources:/app/classes:/app/libs/*", "$mainClassName"
    }
}
assemble.dependsOn(dockerBuildImage)

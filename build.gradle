// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.60'
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.4.0'
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.8.0.1969"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.25.0"
        classpath "org.owasp:dependency-check-gradle:5.2.4"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.docker-java:docker-java:3.1.5"
        classpath "com.bmuschko:gradle-docker-plugin:6.0.0"
        //Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply plugin: "base"
apply plugin: "org.owasp.dependencycheck"
apply plugin: "org.sonarqube"
apply plugin: "jacoco"
sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
        property "sonar.pitest.mode", "reuseReport"
    }
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
    }
}

configure(javaProjects()) {
    apply plugin: 'eclipse'
    apply plugin: "info.solidsoft.pitest"
    apply plugin: "jacoco"

    eclipse {
        classpath {
            //customizing the classes output directory:
            defaultOutputDir = file('build-eclipse')

            //default settings for downloading sources and Javadoc:
            downloadSources = true
            downloadJavadoc = false
        }
    }
    
    jacoco {
        toolVersion = "0.8.3"
        reportsDir = file("${buildDir}/reports/jacoco")
    }
    def jacocoExecutionDataFiles = fileTree(buildDir).include("/jacoco/*.exec")
    jacocoTestReport {
        executionData.from = jacocoExecutionDataFiles
    }

    pitest {
        targetClasses = ['com.*']
        pitestVersion = "1.4.10" //not needed when a default PIT version should be used
        threads = 4
        outputFormats = ['XML', 'HTML']
        failWhenNoMutations = false
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

        testCompile 'junit:junit:4.12'
        testCompile 'com.openpojo:openpojo:0.8.13'
        testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    }
}

def javaProjects() {
    subprojects - rootProject() - appProjects()
}

def rootProject() {
    [project]
}

def appProjects() {
    subprojects.findAll { it.name.endsWith('app') }
}

task jacocoRootReport(type: JacocoReport) {
    description = 'Generates an aggregate report from all subprojects'

    additionalSourceDirs.from = files(javaProjects()*.sourceSets.main.allSource.srcDirs)
    sourceDirectories.from = files(javaProjects()*.sourceSets.main.allSource.srcDirs)
    classDirectories.from = files(javaProjects()*.sourceSets.main.output)
    executionData.from = files(javaProjects()*.jacocoTestReport.executionData)
    
    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled true          
        html.destination file("${buildDir}/reports/jacoco/report.html")
        csv.enabled false
    }
}

apply plugin: "com.github.ben-manes.versions"
dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

check.dependsOn dependencyUpdates
check.dependsOn jacocoRootReport
jacocoRootReport.dependsOn javaProjects()*.jacocoTestReport


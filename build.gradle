// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.4.10'
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.5.2'
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.0"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.28.0"
        classpath "org.owasp:dependency-check-gradle:6.0.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.docker-java:docker-java:3.1.5"
        classpath "com.bmuschko:gradle-docker-plugin:6.1.4"
        //Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply plugin: "base"
apply plugin: "org.owasp.dependencycheck"
apply plugin: "org.sonarqube"
apply plugin: "jacoco"
sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
        property "sonar.pitest.mode", "reuseReport"
    }
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
    }
}

configure(javaProjects()) {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: "info.solidsoft.pitest"
    apply plugin: "jacoco"

    eclipse {
        classpath {
            //customizing the classes output directory:
            defaultOutputDir = file('build-eclipse')

            //default settings for downloading sources and Javadoc:
            downloadSources = true
            downloadJavadoc = false
        }
    }
    
    jacoco {
        toolVersion = "0.8.5"
        reportsDir = file("${buildDir}/reports/jacoco")
    }
    def jacocoExecutionDataFiles = fileTree(buildDir).include("/jacoco/*.exec")
    jacocoTestReport {
        executionData.from = jacocoExecutionDataFiles
    }

    pitest {
        targetClasses = ['com.*']
        pitestVersion = "1.4.10" //not needed when a default PIT version should be used
        threads = 4
        outputFormats = ['XML', 'HTML']
        failWhenNoMutations = false
    }
    
    test {
        useJUnitPlatform()
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

        testImplementation 'junit:junit:4.13'
        testImplementation 'com.openpojo:openpojo:0.8.13'
        testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
        testImplementation 'org.mockito:mockito-junit-jupiter:3.5.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.6.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
        testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.6.2'
    }
}

def javaProjects() {
    subprojects - rootProject() - appProjects()
}

def rootProject() {
    [project]
}

def appProjects() {
    subprojects.findAll { it.name.endsWith('app') }
}

task jacocoRootReport(type: JacocoReport) {
    description = 'Generates an aggregate report from all subprojects'

    additionalSourceDirs.from = files(javaProjects()*.sourceSets.main.allSource.srcDirs)
    sourceDirectories.from = files(javaProjects()*.sourceSets.main.allSource.srcDirs)
    classDirectories.from = files(javaProjects()*.sourceSets.main.output)
    executionData.from = files(javaProjects()*.jacocoTestReport.executionData)
    
    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled true          
        html.destination file("${buildDir}/reports/jacoco/report.html")
        csv.enabled false
    }
}

apply plugin: "com.github.ben-manes.versions"
dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

check.dependsOn dependencyUpdates
check.dependsOn jacocoRootReport
jacocoRootReport.dependsOn javaProjects()*.jacocoTestReport

